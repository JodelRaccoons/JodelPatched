import com.android.build.gradle.tasks.Lint
import javax.swing.*

// DexPatcher Patched Application plugin:
apply plugin: 'com.github.lanchon.dexpatcher.patched-application'

// The DexPatcher Patched Application plugin produces a modified Android application
// by applying changes and additions to code and resources of a source application
// imported as a DexPatcher APK library.

// General Plugin Configuration
dexpatcherConfig {
    // This should point to a local clone of the DexPatcher Gradle plugin support tool
    // repo that is available here: https://github.com/Lanchon/DexPatcher-gradle-tools
    // But instead of defining the directory here, add a 'dexpatcher.dir' entry to the
    // 'local.properties' of the root project. That way it becomes available to all
    // subprojects and the local configuration is not posted to the VCS.
    //dir = '~/android/dexpatcher/dexpatcher-gradle-tools'

    // Specific Plugin Configuration
    patchedApplication {
        // Speed up debug builds by enabling multi-threaded multi-dex processing for them,
        // but only if multi-dex mode is already enabled for them. Defaults to true.
        //multiDexThreadedForMultiDexDebugBuilds = true

        // Speed up debug builds by enabling multi-threaded multi-dex processing for them,
        // even if multi-dex mode is not already enabled for them. Note that apps patched
        // in multi-dex mode are only compatible with devices running Android 5.0 (API 21)
        // and later. Defaults to false.
        //multiDexThreadedForAllDebugBuilds = true
    }

    // Per-Tool Defaults
    dexpatcher {
        // The Android api level of dex files. The api level is auto-detected by DexPatcher
        // v1.2.0 and later, so this setting is no longer needed for normal operation.
        //apiLevel = 14

        // Enable multi-dex support. Note that DexPatcher only supports native multi-dex as
        // implemented in ART and does not support legacy multi-dex for Dalvik VM. Patched
        // multi-dex apps are only compatible with Android 5.0 (API 21) and later devices.
        //multiDex = true

        // Enable multi-threaded multi-dex processing, if multi-dex mode is already enabled.
        // Multi-threaded processing is faster but produces more dex files than the bare
        // minimum required by the contents of the patched app. This is recommended for
        // debug builds but not for releases.
        //multiDexThreaded = true

        // Set the number of multi-dex processing threads, if multi-threaded multi-dex mode
        // is already enabled. Defaults to the number of available processors up to 4.
        //multiDexJobs = 8

        // The maximum number of items in the pools of the output dex files. This limits,
        // for example, the number of method references in each dex file. Defaults to 65536.
        //maxDexPoolSize = 65536

        // The package name of the used DexPatcher annotations. Useful to handle rogue apps
        // that want to disallow patching by intentionally clashing with standard DexPatcher
        // annotation names. Defaults to 'lanchon.dexpatcher.annotation'.
        //annotationPackage = 'lanchon.dexpatcher.annotation'

        // Enable compatibility with the deprecated DexTag.
        //compatDexTag = true

        // The verbosity of the DexPatcher tool. One of QUIET, NORMAL, VERBOSE or DEBUG.
        // Defaults to NORMAL, but VERBOSE is recommended.
        verbosity = VERBOSE

        // Enable logging of relative paths of source files instead of just their filename.
        //logSourcePath = true

        // Enable logging of paths of source files prepending the specified root.
        //logSourcePathRoot = file('src/main/java')

        // Enable logging of timing statistics.
        //logStats = true

    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        // The 'package' attribute in the patched app manifest must be different from the
        // source app ID to support class generation (R and BuildConfig), but here you can
        // override the exported patched app ID to match the source.
        applicationId 'com.tellm.android.app'

        // You must set these fields to match or be compatible with the corresponding data
        // of the source app. (Data is displayed when the source apk library is built.)
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 1003001
        versionName '4.59.1'
        setProperty("archivesBaseName", "JodelPatched-$versionName")
    }

    signingConfigs {
        release {
            storeFile file("Keystore.keystore")
            keyAlias "jodelpatched"
            storePassword ''
            keyPassword ''
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}

tasks.withType(Lint) {
    enabled = false
}

dependencies {
//    compile fileTree(include: ['*.jar'], dir: 'libs')
    // To produce a patched application we need to import an apk library created from the
    // app we want to modify. In this case we import the artifact produced by the 'source'
    // subproject. But the library could be produced by an independent project and imported
    // manually in Android Studio just like any other android library (aar) file.
    compile project(':source')
    // We import an Android library, a file chooser, just for fun. This pulls in all its
    // resources that will be transparently merged, like other resources from this project,
    // with those of the source app. Each component will have its own independent 'R' class.
    // The numeric IDs of the resources coming from the source app will be kept constant.
    //    compile('ru.bartwell:exfilepicker:1.8') {
    //        // But the imported library has a transitive dependency on a module that is already
    //        // present in the source app. So we exclude it from the import, keeping the source
    //        // app's version. Alternatively we could delete the copy brought in by the source
    //        // app using DexPatcher package annotations.
    //        exclude module: 'support-v4'
    //    }
    debugCompile 'com.facebook.stetho:stetho:1.5.0'
}
repositories {
    mavenCentral()
}
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':patched:assembleRelease')) {
        String keyPass = ''
        if (System.console() == null) {
            keyPass = (String) JOptionPane.showInputDialog(
                    "Signing keystore password: ")
        } else {
            keyPass = System.console().readPassword("\nSigning keystore password: ")
        }

        if (keyPass.size() <= 0) {
            throw new Exception("You must provide a password with non-zero length")
        }

        android.signingConfigs.release.storePassword = keyPass
        android.signingConfigs.release.keyPassword = keyPass
    }
}